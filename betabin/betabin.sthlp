{smcl}
{* *! version 1.0.0  20may2012}{...}
{cmd:help betabin}{right: ({browse "http://www.stata-journal.com/article.html?article=up0058":SJ18-1: st0337_1})}
{hline}

{title:Title}

{p2colset 5 16 18 2}{...}
{p2col :{cmd:betabin} {hline 2}}Beta-binomial regression{p_end}
{p2colreset}{...}


{marker syntax}{...}
{title:Syntax}

{p 8 15 2}
{cmd:betabin} {depvar} [{indepvars}] {ifin} {weight}{cmd:,}
	{opt n:}{cmd:(}{it:varname_n}{cmd:)} [{it:{help betabin##betabin_options:betabin_options}}]


{synoptset 28 tabbed}{...}
{marker betabin_options}{...}
{synopthdr :betabin_options}
{synoptline}
{p2coldent :* {opt n(varname_n)}}binomial denominator; you may also specify a scalar as the argument{p_end}
{synopt :{opt nocon:stant}}suppress constant term{p_end}
{synopt :{opth e:xposure(varname:varname_e)}}include ln({it:varname_e}) in model with coefficient constrained to 1{p_end}
{synopt :{opth off:set(varname:varname_o)}}include {it:varname_o} in model with coefficient constrained to 1{p_end}
{synopt :{cmdab:const:raints(}{it:{help estimation options##constraints():constraints}}{cmd:)}}apply specified linear constraints{p_end}
{synopt:{opt col:linear}}keep collinear variables{p_end}
{synopt :{opt l:ink(linkname)}}specify the link function for the binomial model ({cmd:logit}, {cmd:probit}, {cmd:loglog}, {cmd:cloglog}){p_end}
{synopt :{opth vce(vcetype)}}{it:vcetype} may be {opt oim},
{opt r:obust}, {opt cl:uster} {it:clustvar}, {opt opg}, {opt boot:strap},
or {opt jack:knife}{p_end}
{synopt :{opt l:evel(#)}}set confidence level; default is {cmd:level(95)}{p_end}
{synopt :{opt nolr}}suppress likelihood-ratio test{p_end}
{synopt :{opt eform}}report exponentiated coefficients in the binomial model{p_end}
{synopt :{opt nocnsr:eport}}do not display constraints{p_end}
{synopt :{it:{help betabin##betabin_display_options:display_options}}}control
INCLUDE help shortdes-displayoptall
{synopt :{it:{help betabin##betabin_maximize:maximize_options}}}control the maximization process; seldom used{p_end}
INCLUDE help shortdes-coeflegend
{synoptline}
{p2colreset}{...}
{p 4 6 2}
* {opt n:}{cmd:(}{it:varname_n}{cmd:)} is required.{p_end}
INCLUDE help fvvarlist2
{p 4 6 2} {it:depvar}, {it:indepvars}, {it:varname_e}, and {it:varname_o} may
contain time-series operators ({cmd:betabin} only); see {help tsvarlist}.{p_end}
{p 4 6 2} {opt bootstrap}, {opt by} ({cmd:betabin} only), {opt fracpoly}
({cmd:betabin} only), {opt jackknife}, {opt mfp} ({cmd:betabin} only),
{opt mi estimate}, {opt nestreg} ({cmd:betabin} only), {opt rolling},
{opt statsby}, {opt stepwise}, and {opt svy} are allowed; see {help prefix}.
{p_end}
INCLUDE help vce_mi
{p 4 6 2}Weights are not allowed with the {helpb bootstrap} prefix.{p_end}
{p 4 6 2}
{opt vce()} and weights are not allowed with the {helpb svy}
prefix.{p_end}
{p 4 6 2}{cmd:fweight}s, {cmd:iweight}s, and {cmd:pweight}s are allowed; see
{help weight}.{p_end}
{p 4 6 2}See {help betabin_postestimation:betabin postestimation} for features
available after estimation.{p_end}


{marker description}{...}
{title:Description}

{pstd}
{cmd:betabin} fits a beta-binomial regression model of {depvar} on
{indepvars}, where {it:depvar} is a nonnegative count variable.  In this
model, the count variable is believed to be generated by a binomial process,
except that the variation is greater than that of a true binomial.  This extra
variation is referred to as beta-binomial overdispersion.  See 
{manhelp poisson R}.


{title:Options}

{phang}
{opt n(varname_n)} specifies the binomial denominator.  You may specify
a scalar as the argument.  {cmd:n()} is required.

{phang}
{opt noconstant}; see
{helpb estimation options##noconstant:[R] estimation options}.

{phang}
{opth "exposure(varname:varname_e)"}, {opt offset(varname_o)},
{opt constraints(constraints)}, {opt collinear}; see
{helpb estimation options:[R] estimation options}.

{phang}
{opt link(linkname)} specifies the link function for the binomial model
({cmd:logit}, {cmd:probit}, {cmd:loglog}, {cmd:cloglog}).

INCLUDE help vce_asymptall

{phang}
{opt level(#)}; see
{helpb estimation options##level():[R] estimation options}.

{phang}
{opt nolr} suppresses fitting the binomial model.  Without this option, a
comparison binomial model is fit, and the likelihood is used in a
likelihood-ratio test of the null hypothesis that the dispersion parameter is
zero.

{phang}
{opt eform} reports estimated coefficients transformed to exponentiated
coefficients.  Standard errors and confidence intervals are similarly
transformed.  This option affects how results are displayed, not how they are
estimated or stored.  {opt eform} may be specified at estimation or when
replaying previously estimated results.

{phang}
{opt nocnsreport}; see 
{helpb estimation options##nocnsreport:[R] estimation options}.

{phang}
{it:display_options}:
{opt noomit:ted},
{opt vsquish},
{opt noempty:cells},
{opt base:levels},
{opt allbase:levels},
{opth cformat(%fmt)},
{opt pformat(%fmt)},
{opt sformat(%fmt)}, and
{opt nolstretch};
    see {helpb estimation options##display_options:[R] estimation options}.

{phang}
{it:maximize_options}: {opt dif:ficult},
{opth tech:nique(maximize##algorithm_spec:algorithm_spec)},
{opt iter:ate(#)}, [{cmdab:no:}]{opt lo:g}, {opt tr:ace},
{opt grad:ient}, {opt showstep}, {opt hess:ian},
{opt showtol:erance}, {opt tol:erance(#)}, {opt ltol:erance(#)},
{opt nrtol:erance(#)}, {opt nonrtol:erance}, and
{opt from(init_specs)}; see {manhelp maximize R}.  These options are seldom
used.

{pmore}
Setting the optimization type to {cmd:technique(bhhh)} resets the default
{it:vcetype} to {cmd:vce(opg)}.

{phang}
{opt coeflegend}; see
{helpb estimation options##coeflegend:[R] estimation options}.


{marker saved_results}{...}
{title:Stored results}

{pstd}
{cmd:betabin} stores the following in {cmd:e()}:

{synoptset 20 tabbed}{...}
{p2col 5 20 24 2: Scalars}{p_end}
{synopt:{cmd:e(N)}}number of observations{p_end}
{synopt:{cmd:e(k)}}number of parameters{p_end}
{synopt:{cmd:e(k_aux)}}number of auxiliary parameters{p_end}
{synopt:{cmd:e(k_eq)}}number of equations in {cmd:e(b)}{p_end}
{synopt:{cmd:e(k_eq_model)}}number of equations in overall model test{p_end}
{synopt:{cmd:e(k_dv)}}number of dependent variables{p_end}
{synopt:{cmd:e(df_m)}}model degrees of freedom{p_end}
{synopt:{cmd:e(r2_p)}}pseudo-R-squared{p_end}
{synopt:{cmd:e(ll)}}log likelihood{p_end}
{synopt:{cmd:e(ll_0)}}log likelihood, constant-only model{p_end}
{synopt:{cmd:e(ll_c)}}log likelihood, comparison model{p_end}
{synopt:{cmd:e(sigma)}}value of sigma{p_end}
{synopt:{cmd:e(N_clust)}}number of clusters{p_end}
{synopt:{cmd:e(chi2)}}chi-squared{p_end}
{synopt:{cmd:e(chi2_c)}}chi-squared for comparison test{p_end}
{synopt:{cmd:e(p)}}significance{p_end}
{synopt:{cmd:e(rank)}}rank of {cmd:e(V)}{p_end}
{synopt:{cmd:e(rank0)}}rank of {cmd:e(V)} for constant-only model{p_end}
{synopt:{cmd:e(ic)}}number of iterations{p_end}
{synopt:{cmd:e(bin_n)}}sum of the numerators of the outcomes{p_end}
{synopt:{cmd:e(bin_d)}}sum of the denominators of the outcomes{p_end}
{synopt:{cmd:e(rc)}}return code{p_end}
{synopt:{cmd:e(converged)}}{cmd:1} if converged, {cmd:0} otherwise{p_end}

{synoptset 20 tabbed}{...}
{p2col 5 20 24 2: Macros}{p_end}
{synopt:{cmd:e(cmd)}}{cmd:betabin}{p_end}
{synopt:{cmd:e(cmdline)}}command as typed{p_end}
{synopt:{cmd:e(depvar)}}name of dependent variable{p_end}
{synopt:{cmd:e(link)}}name of the link function{p_end}
{synopt:{cmd:e(wtype)}}weight type{p_end}
{synopt:{cmd:e(wexp)}}weight expression{p_end}
{synopt:{cmd:e(title)}}title in estimation output{p_end}
{synopt:{cmd:e(clustvar)}}name of cluster variable{p_end}
{synopt:{cmd:e(offset)}}linear offset variable ({cmd:betabin}){p_end}
{synopt:{cmd:e(offset1)}}linear offset variable ({cmd:gbetabin}){p_end}
{synopt:{cmd:e(chi2type)}}{cmd:Wald} or {cmd:LR}; type of model chi-squared
	test{p_end}
{synopt:{cmd:e(chi2_ct)}}{cmd:Wald} or {cmd:LR}; type of model chi-squared
	test corresponding to {cmd:e(chi2_c)}{p_end}
{synopt:{cmd:e(dispers)}}{cmd:mean} or {cmd:constant}{p_end}
{synopt:{cmd:e(vce)}}{it:vcetype} specified in {cmd:vce()}{p_end}
{synopt:{cmd:e(vcetype)}}title used to label Std. Err.{p_end}
{synopt:{cmd:e(opt)}}type of optimization{p_end}
{synopt:{cmd:e(which)}}{cmd:max} or {cmd:min}; whether optimizer is to perform
                         maximization or minimization{p_end}
{synopt:{cmd:e(ml_method)}}type of {cmd:ml} method{p_end}
{synopt:{cmd:e(user)}}name of likelihood-evaluator program{p_end}
{synopt:{cmd:e(technique)}}maximization technique{p_end}
{synopt:{cmd:e(properties)}}{cmd:b V}{p_end}
{synopt:{cmd:e(predict)}}program used to implement {cmd:predict}{p_end}
{synopt:{cmd:e(asbalanced)}}factor variables {cmd:fvset} as {cmd:asbalanced}{p_end}
{synopt:{cmd:e(asobserved)}}factor variables {cmd:fvset} as {cmd:asobserved}{p_end}

{synoptset 20 tabbed}{...}
{p2col 5 20 24 2: Matrices}{p_end}
{synopt:{cmd:e(b)}}coefficient vector{p_end}
{synopt:{cmd:e(Cns)}}constraints matrix{p_end}
{synopt:{cmd:e(ilog)}}iteration log (up to 20 iterations){p_end}
{synopt:{cmd:e(gradient)}}gradient vector{p_end}
{synopt:{cmd:e(V)}}variance-covariance matrix of the estimators{p_end}
{synopt:{cmd:e(V_modelbased)}}model-based variance{p_end}

{synoptset 20 tabbed}{...}
{p2col 5 20 24 2: Functions}{p_end}
{synopt:{cmd:e(sample)}}marks estimation sample{p_end}
{p2colreset}{...}

{marker examples}{...}
{title:Examples}

{pstd}Setup{p_end}
{phang2}{cmd:. import excel "https://raw.githubusercontent.com/timbulwidodostp/betabin/main/betabin/betabin.xlsx", sheet("Sheet1") firstrow clear}{p_end}

{pstd}Beta-binomial regression{p_end}
{phang2}{cmd:. betabin Dependen Independen_1 Independen_2 Independen_3, n(Independen_4)}{p_end}

{title:Author}

{pstd}Timbul Widodo{p_end}
{pstd}www.youtube.com/@amalsedekah{p_end}

{pstd}James W. Hardin{p_end}
{pstd}Institute for Families in Society{p_end}
{pstd}Department of Epidemiology and Biostatistics{p_end}
{pstd}University of South Carolina{p_end}
{pstd}Columbia, SC{p_end}
{pstd}jhardin@sc.edu{p_end}
{marker author}{...}


{title:Also see}

{p 4 14 2}Article:  {it:Stata Journal}, volume 18, number 1: {browse "http://www.stata-journal.com/article.html?article=up0058":st0337_1},{break}
                    {it:Stata Journal}, volume 14, number 2: {browse "http://www.stata-journal.com/article.html?article=st0337":st0337}

{p 7 14 2}Help:  {help betabin postestimation} (if installed),
{manhelp estimation MI}, {manhelp glm R}, 
{manhelp svy_estimation SVY:svy estimation}{p_end}
